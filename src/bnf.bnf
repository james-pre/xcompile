
string          = '("(?:\\.|[^"\\])*"|\'(?:\\.|[^\'\\])*\')';
identifier      = '[a-zA-Z_]\w*';
whitespace      = '[ \t]+';
line_terminator	= "\n+";
comment         = "#(?!#)([^\n]*)";
directive       = "##\w+ [^\n]*";

# Simplifies internals

pipe = '\|';
left_paren = '\(';
right_paren = '\)';
left_bracket = '\[';
right_bracket = '\]';
left_brace = '\{';
right_brace = '\}';

##ignore whitespace comment line_terminator

parenthesized = left_paren, expression, right_paren;
optional = left_bracket, expression, right_bracket;
repetition = left_brace, expression, right_brace;

group = parenthesized | optional | repetition;

factor = string | identifier | group;

term = factor, {',', factor};

expression = term, {pipe, term};

rule = identifier, '=', expression, ';';

rule_list = (rule | directive), {rule | directive};

##root rule_list
